import deps.Application
import deps.Dependencies
import deps.Versions
import deps.dependencies.AndroidTest
import deps.dependencies.AndroidX
import deps.dependencies.Test

plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'
  id 'kotlin-parcelize'
  id 'com.google.devtools.ksp' version '1.6.10-1.0.2'
}

kotlin {
  sourceSets {
    debug {
      kotlin.srcDir("build/generated/ksp/debug/kotlin")
    }
    release {
      kotlin.srcDir("build/generated/ksp/release/kotlin")
    }
  }
}

android {
  namespace 'me.darthwithap.android.stockmarketapp'
  compileSdk Application.compileSdk

  defaultConfig {
    applicationId Application.id
    minSdk Application.minSdk
    targetSdk Application.targetSdk
    versionCode Application.versionCode
    versionName Application.versionName

    testInstrumentationRunner AndroidTest.instrumentationTest
    vectorDrawables {
      useSupportLibrary true
    }
  }

  applicationVariants.all { variant ->
    kotlin.sourceSets {
      getByName(variant.name) {
        kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  buildFeatures {
    compose true
  }
  composeOptions {
    kotlinCompilerExtensionVersion Versions.compose
  }
  packagingOptions {
    resources {
      excludes += '/META-INF/{AL2.0,LGPL2.1}'
    }
  }
}

dependencies {
  implementation AndroidX.lifecycleRuntimeKtx
  implementation AndroidX.appcompat
  implementation Dependencies.material
  implementation AndroidX.constraintLayout
  implementation Dependencies.accompanistFlowLayout
  implementation Dependencies.accompanistSwipeRefresh
  coreLibraryDesugaring Dependencies.desugar

  //OpenCSV
  implementation Dependencies.opencsv

  //Compose dependencies
  implementation AndroidX.composeUi
  implementation AndroidX.composeMaterial
  implementation AndroidX.composeActivity
  implementation AndroidX.composeNavigation
  implementation AndroidX.composePaging
  implementation AndroidX.composeUiToolingPreview
  implementation AndroidX.composeLifecycleViewModel
  implementation AndroidX.composeMaterialIconsExtended

  //Compose Nav Destinations
  implementation Dependencies.composeDestinationsCore
  ksp Dependencies.composeDestinationsKsp

  // Coil
  implementation Dependencies.coil

  //Dagger - Hilt
  implementation Dependencies.hiltAndroid
  kapt Dependencies.hiltAndroidCompiler
  kapt AndroidX.hiltCompiler
  implementation AndroidX.hiltNavigationCompose

  // Retrofit
  implementation Dependencies.retrofit
  implementation Dependencies.moshiConvertor
  implementation Dependencies.okhttp
  implementation Dependencies.loggingInterceptor

  // Room
  implementation Dependencies.room
  kapt Dependencies.roomCompiler

  // Kotlin Extensions and Coroutines support for Room
  implementation AndroidX.roomKtx

  //Testing
  testImplementation Test.junit
  androidTestImplementation AndroidTest.junit
  androidTestImplementation AndroidTest.espresso
  androidTestImplementation AndroidTest.composeUiTest
  debugImplementation AndroidX.composeUiTooling
}